; 3Q MAS pulse program for half integer spin nuclei
; experiment with zero quantum filter:
; excitation - t1(3Q) - conversion -  tau(zqf) - 90sel - acquisition

;ns : 24 * n
;d1 : recycle delay
;p1 : excitation pulse at pl11 (long /checked)
;p2 : conversion pulse at pl11 (short / checked)
;p3 : 90 degree selective pulse at pl21
;pl1 : =120 dB
;pl11 : power level for excitation and conversion pulses
;pl21 : power level for selective pulse
;cnst31 : spinning speed
;d0 : =1u or longer
;in0 : 1 rotation period for synchronised experiment
;td1 : number of t1-experiments / 2
;FnMODE : States
;MC2 : States
;pl12 : power level for decoupling
;p31 : tppm pulse (~T180)
;cpdprg2 : cw or tppm if needed
;zgoptn : -Ddec if needed

;$COMMENT=Z-filtered 3QMAS
;$CLASS=Solids
;$DIM=2D
;$TYPE=half integer quadrupoles
;$SUBTYPE=MQMAS

define delay Zfilter
"Zfilter=l30*(1s/cnst31)-(p2/2)-(p3/2)"

;"d10=d0-p1/2-p2/2" /* to be set to do strict t1 synchronization */
"d0= 1.0u"

"in0=inf1"

  ze
1 d1 do:f2
;"d10=d0-p1/2-p2/2" /* to be set to do strict t1 synchronization */
  (p1 pl11 ph1):f1		; excitation
#ifdef dec
  1u cpds2:f2
  d0			; t1 evolution
  1u do:f2
#else
  d0
#endif
  (p2 pl11 ph2):f1		; reconversion
  Zfilter							; Z filter
  (p3 pl21 ph3):f1
#ifdef dec
  1u cpds2:f2
  go=1 ph31		; acquisition
  1m do:f2
#else
  go=1 ph31		; acquisition
#endif
  30m mc #0 to 1 F1PH(ip1,id0)
  HaltAcqu, 1m
exit

ph1=(12) 0	; 0 = Cosine data for spin 5/2 & 7/2 (checked)
ph2=(6)  0 1 2 3 4 5
ph3=     0 0 0 0 0 0  2 2 2 2 2 2  1 1 1 1 1 1  3 3 3 3 3 3
ph31=    0 2 0 2 0 2  2 0 2 0 2 0  1 3 1 3 1 3  3 1 3 1 3 1



















